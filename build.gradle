buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id "maven-publish"
    id "java"
    id "signing"
    id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
}

group 'com.nickhudkins'
version '1.4.0'

sourceCompatibility = 1.8

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

repositories {
    mavenLocal()
    mavenCentral()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "knezmilos13"
            name "Milos Milutinovic"
            email "knezmilos13@gmail.com"
        }
    }

    scm {
        url "https://github.com/nickhudkins/prettyboxformatter"
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            def releaseRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo : snapshotRepo
            credentials {
                username = System.getenv("ORG_GRADLE_PROJECT_sonatypeUsername")
                password = System.getenv("ORG_GRADLE_PROJECT_sonatypePassword")
            }
        }
    }
    publications {
        mavenPublication(MavenPublication) {
            groupId 'com.nickhudkins'
            artifactId 'prettyboxformatter'
            version findProperty('version')
            from components.java

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A library for formatting/serializing text into boxes drawn using UTF-8 characters.')
                root.appendNode('name', 'Pretty Box Formatter')
                root.appendNode('url', 'https://github.com/nickhudkins/prettyboxformatter')
                root.children().last() + pomConfig
            }

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

signing {
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation group: 'org.jetbrains', name: 'annotations', version: '13.0'
}
